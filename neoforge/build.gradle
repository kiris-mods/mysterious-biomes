plugins {
	id 'idea'
	id 'maven-publish'
	id 'net.neoforged.gradle.userdev' version '7.0.57'
}

base {
	archivesName = "${mod_id}-neoforge-${minecraft_version}"
}

// Automatically enable neoforge AccessTransformers if the file exists
// This location is hardcoded in FML and can not be changed.
// https://github.com/neoforged/FancyModLoader/blob/a952595eaaddd571fbc53f43847680b00894e0c1/loader/src/main/java/net/neoforged/fml/loading/moddiscovery/ModFile.java#L118
if (file('src/main/resources/META-INF/accesstransformer.cfg').exists()) {
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

runs {
	configureEach {
		modSource project.sourceSets.main
		systemProperty 'forge.logging.markers', 'REGISTRIES'
		systemProperty 'forge.logging.console.level', 'debug'
	}

	client {
		if (project.hasProperty('mc_uuid')) {
			// Your UUID, trimmed / without the dashes
			programArgument "--uuid=${project.mc_uuid}"
		}

		if (project.hasProperty('mc_username')) {
			// Your Minecraft in-game username, not email
			programArgument "--username=${project.mc_username}"
		}
	}

	server {
		programArgument '--nogui'
	}

	data {
		programArguments.addAll '--mod', mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
	}
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
	implementation "net.neoforged:neoforge:${neoforge_version}"
	compileOnly project(":common")
	implementation "com.github.glitchfiend:TerraBlender-neoforge:${terrablender_version_neoforge}"
	//implementation "dev.tophatcat.kirisutils:kirisutils-neoforge-${minecraft_version}:${kirisutils_version_neoforge}"
	implementation "software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version_neoforge}"
}

// NeoGradle compiles the game, but we don't want to add our common code to the game's code
Spec<Task> notNeoTask = { Task it -> !it.name.startsWith("neo") } as Spec<Task>

tasks.withType(JavaCompile).matching(notNeoTask).configureEach {
	source(project(":common").sourceSets.main.allSource)
}

tasks.withType(Javadoc).matching(notNeoTask).configureEach {
	source(project(":common").sourceSets.main.allJava)
}

tasks.named("sourcesJar", Jar) {
	from(project(":common").sourceSets.main.allSource)
}

tasks.withType(ProcessResources).matching(notNeoTask).configureEach {
	from project(":common").sourceSets.main.resources
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId base.archivesName.get()
			from components.java
		}
	}
	repositories {
		maven {
			url "file://" + System.getenv("local_maven")
		}
	}
}
